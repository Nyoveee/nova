#version 450 core
layout (local_size_x = 128, local_size_y = 1, local_size_z = 1) in;

struct ParticleVertex{
	vec4 color;
	vec3 position;
	float rotation;
	float currentSize;
};
struct Particle{
    vec4 startColor;
    vec4 endColor;
    vec3 velocity;
    vec3 direction;
    vec3 force;
    vec3 lightattenuation;
    float colorInterpolation;
    float sizeInterpolation;
    float lightIntensity;
    float lightRadius;
    float angularVelocity;
    float startSize;
    float endSize;
    float currentLifeTime;
    float lifeTime;
    bool colorOverLifetime;
    bool sizeOverLifetime;
    bool b_Active;
};

layout(std430, binding = 4) buffer ParticleVertexList {
    ParticleVertex particleVertices[];
};

layout(std430, binding = 5) buffer ParticleList {
    int particleCount;
    int newParticleIndex;
    Particle particles[];
};


uniform float dt;  

float InterpolationFloat(float start, float end, float t, float degree){
    return start + (end - start) * pow(t, 1.f / degree);
}

vec4 InterpolationVector(vec4 start, vec4 end, float t, float degree) {
    float x = InterpolationFloat(start.x, end.x, t, degree);
    float y = InterpolationFloat(start.y, end.y, t, degree);
    float z = InterpolationFloat(start.z, end.z, t, degree);
    float w = InterpolationFloat(start.w, end.w, t, degree);
    return vec4(x,y,z,w);
}

void main() {
    uint particleIndex = gl_GlobalInvocationID.x;
    if(particleIndex >= particleCount)
        return;
    particles[particleIndex].currentLifeTime -= dt;
    // Set this particle to be removed once it reach end of lifetime
    if (particles[particleIndex].currentLifeTime <= 0) {
        particles[particleIndex].b_Active = false;
	    return;
    }
    // Movement
    particleVertices[particleIndex].position += particles[particleIndex].velocity * dt;
    particles[particleIndex].velocity += particles[particleIndex].force * dt;
    particleVertices[particleIndex].rotation += particles[particleIndex].angularVelocity * dt;
    // Color
    if (particles[particleIndex].colorOverLifetime) {
	    float interpolationValue = 1 - particles[particleIndex].currentLifeTime / particles[particleIndex].lifeTime;
	    float degree = particles[particleIndex].colorInterpolation;
	    particleVertices[particleIndex].color = InterpolationVector(particles[particleIndex].startColor, 
                                                                    particles[particleIndex].endColor, 
                                                                    interpolationValue, 
                                                                    degree);
    }
    // Size
    if (particles[particleIndex].sizeOverLifetime) {
	    float interpolationValue = 1 - particles[particleIndex].currentLifeTime / particles[particleIndex].lifeTime;
	    float degree = particles[particleIndex].sizeInterpolation;
	    particleVertices[particleIndex].currentSize = InterpolationFloat(particles[particleIndex].startSize, 
                                                                         particles[particleIndex].endSize, 
                                                                         interpolationValue, 
                                                                         degree);
    }
}
